plugins {
	id 'idea'
	id 'eclipse'
	id 'java-library'
	id 'maven-publish'
	id 'org.ajoberstar.grgit' version '5.2.1'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = "${project_group}"
version = "${project_version}"

eclipse {
	classpath {
		downloadSources = true
	}
}

idea {
	module {
		downloadSources = true
	}
}

compileJava {
	options.encoding = "${compile_charset}"
}

java {
	toolchain.languageVersion.set(JavaLanguageVersion.of(compile_target))
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.squareup.okhttp3:okhttp:${okhttp_version}"
	implementation "org.json:json:${json_version}"
	implementation "org.slf4j:slf4j-api:${slf4j_version}"

	api "org.jetbrains:annotations:${annotation_version}"
	api "com.google.guava:guava:${guava_version}"
}

spotless {
	format 'misc', {
		target '*.gradle', '.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	java {
		ratchetFrom 'origin/dev'

		target 'src/main/java/**/*.java'

		formatAnnotations()
		palantirJavaFormat()
		indentWithTabs()
		endWithNewline()

		licenseHeader '/*\n' +
				' *    Copyright $YEAR Matt Malec, and the Pterodactyl4J contributors\n' +
				' *\n' +
				' *    Licensed under the Apache License, Version 2.0 (the "License");\n' +
				' *    you may not use this file except in compliance with the License.\n' +
				' *    You may obtain a copy of the License at\n' +
				' *\n' +
				' *        http://www.apache.org/licenses/LICENSE-2.0\n' +
				' *\n' +
				' *    Unless required by applicable law or agreed to in writing, software\n' +
				' *    distributed under the License is distributed on an "AS IS" BASIS,\n' +
				' *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n' +
				' *    See the License for the specific language governing permissions and\n' +
				' *    limitations under the License.\n' +
				' * \n' +
				' *    ============================================================================== \n' +
				' * \n' +
				' *    Copyright 2024 RaftDev, and the Pelican4J contributors\n' +
				' * \n' +
				' *    Licensed under the Apache License, Version 2.0 (the "License");\n' +
				' *    you may not use this file except in compliance with the License.\n' +
				' *    You may obtain a copy of the License at\n' +
				' *\n' +
				' *        http://www.apache.org/licenses/LICENSE-2.0\n' +
				' */\n\n'

	}
}


javadoc {
	failOnError = false
	options {
		memberLevel = JavadocMemberLevel.PUBLIC
		encoding = "${compile_charset}"
	}

	exclude("be/raft/pelican/utils/Checks.java",
			"be/raft/pelican/utils/ExceptionUtils.java",
			"be/raft/pelican/utils/LockUtils.java",
			"be/raft/pelican/utils/NamedThreadFactory.java",

			"be/raft/pelican/entities/impl",
			"be/raft/pelican/application/entities/impl",
			"be/raft/pelican/client/entities/impl",

			"be/raft/pelican/client/ws/handle",
			"be/raft/pelican/client/ws/WebSocketClient.java",
			"be/raft/pelican/client/ws/WebSocketAction.java")
}

publishing {
	boolean isSnapshot = System.getenv("RELEASE") != 'true';

	repositories {
		maven {
			name 'AtlasWorld-Repo'

			def release = "https://repository.atlasworld.fr/repository/maven-release/"
			def snapshot = "https://repository.atlasworld.fr/repository/maven-snapshot/"
			url = isSnapshot ? snapshot : release

			credentials {
				username = System.getenv("AW_REPO_USERNAME")
				password = System.getenv("AW_REPO_PASSWORD")
			}

			metadataSources {
				gradleMetadata()
			}
		}
	}
	publications {
		Pelican4J(MavenPublication) {
			from components.java

			def snapshot = "${project_version}-${grgit.head().id.subSequence(0, 5)}"

			try {
				version = isSnapshot ? snapshot : grgit.tag.list().get(0).getName();
			} catch (IndexOutOfBoundsException e) {
				version = "undefined"
			}

			pom {
				name = 'Pelican4J'
				description = "Pelican Panel API wrapper for java, based of Matt Malec's Pterodactyl4J library"
				licenses {
					license {
						name = 'Apache 2.0'
						url = 'https://github.com/Raft08/Pelican4J/blob/master/LICENSE'
					}
				}
			}
		}
	}
}

java {
	withJavadocJar()
	withSourcesJar()
}
